buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // START: FlutterFire Configuration
        classpath 'com.google.gms:google-services:4.3.15'
        // END: FlutterFire Configuration
        // gradle and kotlin version is managed in settings.gradle
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'

ext {
    compileSdkVersion = 35
    targetSdkVersion = 35
    minSdkVersion = 23
    buildToolsVersion = "34.0.0"
}

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"

    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    try {
                        namespace project.group
                    } catch (Exception e) {
                        namespace "com.audioview.generated.${project.name}"
                    }
                }
                
                // Allow :app to manage its own SDK versions if already configured
                // For library plugins, ensure they meet minimum requirements
                if (project.name != 'app') {
                    compileSdkVersion rootProject.ext.compileSdkVersion
                    buildToolsVersion rootProject.ext.buildToolsVersion
                    
                    compileOptions {
                        sourceCompatibility JavaVersion.VERSION_11
                        targetCompatibility JavaVersion.VERSION_11
                    }
                }
            }
            
            if (project.plugins.hasPlugin('kotlin-android') || project.plugins.hasPlugin('org.jetbrains.kotlin.android')) {
                project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                    kotlinOptions {
                        jvmTarget = "11"
                    }
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
